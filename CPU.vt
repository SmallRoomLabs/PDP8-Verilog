`timescale 100ns/10ns

module CPU_tb();
    reg SYSCLK=0;
    reg reset=0,run=0, halt=0;
    wire [11:0] dutBusPC, dutBusData;
    wire instAND, instTAD, instISZ, instDCA, instJMS, instJMP, instIOT, instOPR;

    CPU dut(
        .SYSCLK(SYSCLK),
        .sw_RESET(reset),
        .sw_RUN(run), 
        .sw_HALT(halt),
        .pBusPC(dutBusPC),
        .pBusData(dutBusData),
        .pInstAND(instAND), .pInstTAD(instTAD), .pInstISZ(instISZ), .pInstDCA(instDCA), .pInstJMS(instJMS), .pInstJMP(instJMP), .pInstIOT(instIOT), .pInstOPR(instOPR)
    );
    
    
    always begin
        #1 SYSCLK<=~SYSCLK;
    end

    // Clear/reset the entire system
    initial begin 
        reset=1; #5 reset=0; 
    end  
    
    initial begin 
        #10 run=1; #5 run=0; 
    end

    initial begin
        $dumpfile("CPU.vcd");
        $dumpvars(0,CPU_tb);
        #32000 $finish;
    end

    always @(dut.theSEQUENCER.stepCnt) begin
        if (dut.theSEQUENCER.stepCnt==5'd31) begin $display("Unhandled OP"); $finish; end
        if (dut.theSEQUENCER.stepCnt==5'b00001) begin:PRN    
 `ifdef TRACE
            $display("PC=%04o IRQ,NO,GIE=%d,%d,%d L=%d ACC=%04o MQ=%04o IR=%04o",
                dut.thePC.PC,
                dut.theInterrupt.irqRq, dut.theInterrupt.flgNoInt, dut.theInterrupt.flgGIE,
                dut.link,
                dut.theACC.data,
                dut.theMQ.data,
                dut.busIR 
            );

    `endif
            if (dut.busIR==12'o7402) begin $display("end by HLT"); $finish; end
            if ((dut.theSEQUENCER.running==1'b1) && (dut.theACC.data[0:0]===1'bx)) begin $display("X in theACC.data"); $finish; end
            if ((dut.theSEQUENCER.running==1'b1) && (dut.theACC.data[0:0]===1'bz)) begin $display("Z in theACC.data"); $finish; end
        end
        if ((dut.theSEQUENCER.running==1'b1) && ($countbits(dut.busData,'x)>0)) begin $display("X in dut.busRamD"); $finish; end

    end
    

endmodule
